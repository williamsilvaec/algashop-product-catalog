plugins {
	id 'java'
	id 'maven-publish'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.springframework.cloud.contract' version '5.0.0'
	id 'org.asciidoctor.jvm.convert' version '4.0.5'
}

group = 'com.williamsilva.algashop'
version = '0.0.1-SNAPSHOT'
description = 'Product Catalog Microservice'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
	mavenLocal()
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact bootJar
			artifact verifierStubsJar
		}
	}
}

ext {
	set('springCloudVersion', "2025.1.0")
	set('snippetsDir', file("build/generated-snippets"))
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
	mockitoAgent {transitive = false}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.modelmapper:modelmapper:3.2.6'
	implementation 'com.fasterxml.uuid:java-uuid-generator:5.1.1'
	implementation 'org.apache.commons:commons-lang3:3.20.0'

	testImplementation 'org.springframework.boot:spring-boot-starter-data-mongodb-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	mockitoAgent 'org.mockito:mockito-core'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

contracts {
	packageWithBaseClasses = "com.williamsilva.algashop.product.catalog.contract.base"
}

tasks.named('contractTest') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	filter {
		excludeTestsMatching("*IT")
	}
	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

tasks.register('integrationTest', Test) {
	description = 'Run integration tests.'
	group = 'verification'
	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath

	shouldRunAfter test

	useJUnitPlatform()

	testLogging {
		events "passed"
	}

	filter {
		includeTestsMatching "*IT"
		excludeTestsMatching "*Test"
	}
}

asciidoctor {
	dependsOn contractTest
	inputs.dir snippetsDir
	attributes 'snippets' : snippetsDir
	sources {
		include 'index.adoc'
	}
	baseDirFollowsSourceFile()
	outputDir = file("build/docs/asciidoc")
}